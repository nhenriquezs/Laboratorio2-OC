.data

grafo:	.word 999,999,999,999,999,999,999,999
	.word 999,999,2,4,7,999,5,999
	.word 999,2,999,999,6,3,999,8
	.word 999,4,999,999,999,999,6,999
	.word 999,7,6,999,999,999,1,6
	.word 999,999,3,999,999,999,999,7
	.word 999,5,999,6,1,999,999,6
	.word 999,999,8,999,6,7,6,999
dist: 	.word 999,999,999,999,999,999,999,999
selected: 	.word 0,0,0,0,0,0,0,0
prev: 		.word -1,-1,-1,-1,-1,-1,-1,-1
tamano:		.word 8
fuente: 	.word 1
objetivo: 	.word 4
	
salto: .asciiz "\n"
espacio: .asciiz "\t"
suma: .asciiz "+"
igual: .asciiz "="

.text

j main

accederMatriz: ##$a0->fila (i)
	       ##$a1->columna (j)
	addi $sp,$sp,-8
	sw $t0,0($sp)
	sw $t1,4($sp)
	
	la $t0,grafo
	lw $t1,tamano
	
	mul $t1,$t1,$a0
	add $t1,$t1,$a1
	sll $t1,$t1,2
	add $t1,$t1,$t0
	lw $v0,0($t1)
	
	lw $t0,0($sp)
	lw $t1,4($sp)
	addi $sp,$sp,8	
	
	jr $ra

imprimirMatriz:
	lw $t0,tamano
	addi $t1,$0,0
	for00:	
		la $a0,salto
		li $v0,4
		syscall
		beq $t1,$t0,finFor00
		addi $t2,$0,0
		for01:
			beq $t2,$t0,finFor01
			la $a0,($t1)
			la $a1,($t2)
			
			addi $sp,$sp,-4
			sw $ra,0($sp)
			jal accederMatriz
			lw $ra,0($sp)
			addi $sp,$sp,4
			
			la $a0,($v0)
			li $v0,1
			syscall
			la $a0,espacio
			li $v0,4
			syscall
			addi $t2,$t2,1
			j for01
		finFor01:
		
		addi $t1,$t1,1
		j for00
	finFor00:
	jr $ra

dijkstra:
	la $a0, dist
	la $a1, prev
	la $a2, selected
	lw $s0, fuente
	lw $s1, objetivo
	lw $s2,tamano
	
	#selected[fuente] = 1	
	addi $t0,$0,1
	sll $t1,$s0,2
	add $t1,$a2,$t1
	sw $t0,0($t1)
	
	#dist[fuente] = 0;
	addi $t0,$0,0
	sll $t1,$s0,2
	add $t1,$a0,$t1
	sw $t0,0($t1)
	
	addi $s2,$s2,-1 #tamano = 7
	while00:
		sll $t0,$s1,2 #objetivo*4
		add $t0,$a2,$t0
		lw $t1,0($t0) #t1=selected[Objetivo]	
		bnez $t1,finwhile00 #while(selected[objetivo] ==0)
        	addi $t2,$0,999	#min = 999; 
        	addi $t3,$0,0	#m = 0
        	addi $t4,$0,0 	#i=1 
        	
        	for02: #for i=1 hasta 7
        		addi $t4,$t4,1 	#i++
        		
        		#dist[fuente];
        		add $t5,$0,$s0 #t5=fuente
        		sll $t5,$t5,2 # fuente*4
        		add $t5,$a0,$t5
        		lw $t6,0($t5) #t6 = dist[fuente]      
			
        		#grafo[fuente][i];
        		addi $sp,$sp,-12
			sw $a0,0($sp)
			sw $a1,4($sp)
			sw $ra,8($sp)
			la $a0,0($s0) #a0=fuente
			la $a1,0($t4) #a1=i
			jal accederMatriz #grafo[fuente][i]
			la $t7,($v0)#t7=grafo[fuente][i]
			lw $a0,0($sp)
			lw $a1,4($sp)
			lw $ra,8($sp)
			addi $sp,$sp,12
		
      
        		add $t8,$t6,$t7 #$t8 = d = dist[fuente] + grafo[fuente][i];

        		if00: #if (selected[i]==0)
        			sll $t5,$t4,2 #t5=i*4
        			add $t5,$a2,$t5
        			lw $t6,0($t5) #$t6 = selected[i]
        			bnez $t6,finif00 #if (selected[i]==0)
        			
        			if01: #if(d< dist[i])
        				sll $t5,$t4,2 #t5=i*4
        				add $t5,$a0,$t5
        				lw $t6,0($t5) #$t6 = dist[i]
        				bge $t8,$t6,finif01 #if(d< dist[i]) --> $t8<$t6
        				
        				sll $t5,$t4,2 #t5=i*4
        				add $t5,$a0,$t5
        				sw $t8,0($t5) #dist[i]=d
        				
        				sll $t5,$t4,2 #t5=i*4
        				add $t5,$a1,$t5
        				sw $s0,0($t5) #prev[i] = fuente;
        			finif01:
        			if02: #if(min>dist[i])
        				sll $t5,$t4,2 #t5=i*4
        				add $t5,$a0,$t5
        				lw $t6,0($t5) #$t6 = dist[i]
        				ble $t2,$t6,finif00 #if(min>dist[i])
        				add $t2,$0,$t6 #min = dist[i];
        				add $t3,$0,$t4 #m = i; 					
        		finif00:
			add $s0,$0,$t3 #start = m;
			
			addi $t5,$0,1
			sll $t6,$s0,2
			add $t6,$a2,$t6
			sw $t5,0($t6)	#selected[fuente] = 1;	  			    		
        		beq $t4,$s2,finfor02 #for i=1 hasta 7
        		j for02
        	finfor02:
		sll $t0,$s1,2 #objetivo*4
		add $t0,$a2,$t0
		lw $t1,0($t0) #t1=selected[Objetivo]	
		beqz $t1,while00 #while(selected[objetivo] ==0)
	finwhile00:
        sll $t5,$s1,2 #t5=i*4
        add $t5,$a0,$t5
        lw $v0,0($t5) #$v0 = dist[objetivo]	
	#return dist[objetivo];
	jr $ra
	
	
main:
	addi $sp,$sp,-4
	sw $ra,0($sp)
	jal dijkstra
	lw $ra,0($sp)
	addi $sp,$sp,4
		
	la $a0,($v0)
	li $v0,1
	syscall
	
	
	
